add_executable(cmag_browser)
target_common_setup(cmag_browser)
target_find_sources_and_add(cmag_browser)
target_include_directories(cmag_browser PRIVATE ${CMAKE_CURRENT_SOURCE_DIR}/..)
target_link_libraries(cmag_browser PRIVATE Imgui Stb cmag_core glm)
add_subdirectories()
target_setup_vs_folders(cmag_browser)

install(TARGETS cmag_browser)

function(add_asset INPUT_FILE OUTPUT_FILE VARIABLE_NAME)
    header_pack_generate_from_binary(cmag_browser "assets/${INPUT_FILE}" "${OUTPUT_FILE}" "${VARIABLE_NAME}" 16)
    source_group(assets FILES "assets/${INPUT_FILE}")
    source_group(assets/generated FILES "${CMAKE_BINARY_DIR}/generated/generated/${OUTPUT_FILE}")
endfunction()
add_asset(Aller_Rg.ttf font.h cmagBrowserFont)
add_asset(folder.png folder_icon.h folderIconPngBytes)
add_asset(cmake_icon.png cmake_icon.h cmakeIconPngBytes)

function(add_shader INPUT_FILE_NAME OUTPUT_FILE VARIABLE_NAME)
    set(SHADERS_DIR "components/target_graph/shaders")
    set(INPUT_FILE "${SHADERS_DIR}/${INPUT_FILE_NAME}")
    header_pack_generate_from_text(cmag_browser "${INPUT_FILE}" "${OUTPUT_FILE}" "${VARIABLE_NAME}")
    source_group("${SHADERS_DIR}" FILES "${INPUT_FILE}")
    source_group("${SHADERS_DIR}/generated" FILES "${CMAKE_BINARY_DIR}/generated/generated/${OUTPUT_FILE}")
endfunction()
add_shader("vertex_shader.glsl" "vertex_shader.glsl.h" vertexShaderSource)
add_shader("fragment_shader.glsl" "fragment_shader.glsl.h" fragmentShaderSource)
